import org.apache.tools.ant.filters.ReplaceTokens

def fetchGitCommit() {
    try {
        def ref = new File(".git/HEAD").readLines()[0]
        def sha1
        if (ref.startsWith("ref") ){
            sha1 = new File(".git", ref.substring(5)).getText().substring(0,8)
        }
        else {
            sha1 = ref.substring(0,8)
        }
        return sha1
    } catch (e) {
        logger.log(LogLevel.WARN, "Unable to get GIT commit: $e")
        return "<unknown>"
    }
}

ext {
    gitCommit = fetchGitCommit()
}

task ('processDockerfile', type: Copy) {
    from 'src'
    into buildDir
    include '**/*Dockerfile'
    filesMatching("**/*Dockerfile") { details ->
        details.path = "${details.name}"
    }
    includeEmptyDirs = false

    filter(ReplaceTokens, tokens: project.properties.findAll { k,v -> k && v } )

}

processResources.finalizedBy processDockerfile
